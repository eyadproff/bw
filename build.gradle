import org.apache.tools.ant.filters.ReplaceTokens

buildscript {
    repositories {
        flatDir {
            dirs "libs/build"
        }
    }
    dependencies {
        classpath name: 'jsch-0.1.54'
        classpath name: 'groovy-ssh-2.9.0'
        classpath name: 'gradle-ssh-plugin-2.9.0'
    }
}

project.version = '2018.09.2'
group = 'sa.gov.nic.bio'

apply plugin: 'java'
apply plugin: 'application'
apply plugin: "org.hidetake.ssh"

sourceCompatibility = 1.8
targetCompatibility = 1.8

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

mainClassName = 'sa.gov.nic.bio.bw.client.AppEntryPoint'
applicationDefaultJvmArgs = ['-Djava.ext.dirs=C:\\Program Files\\Java\\jdk1.8.0_152\\jre\\lib\\ext;build\\extensions',
                             '-Djava.locale.providers=SPI,CLDR,JRE,HOST',
                             '-Djavafx.preloader=sa.gov.nic.bio.bw.client.preloader.AppPreloader']

repositories {
    flatDir {
        dirs "libs/compile"
        dirs "libs/provided"
    }
}

dependencies {
    // BCL-Utils and Biokit library
    implementation name: "bcl-utils-2018.04.1"
    implementation name: "biokit-library-2018.08.2"

    // font to support arabic in jasper reports
    implementation name: "custom-report-font-1.0.0"

    // JavaFX extension library
    implementation name: "controlsfx-8.40.14"
    
    // Retrofit HTTP client
    implementation name: "retrofit-2.4.0"
    implementation name: "okhttp-3.11.0"
    implementation name: "okio-1.15.0"
    implementation name: "converter-gson-2.4.0"
    implementation name: "gson-2.8.5"

    // Tomcat websocket client
    implementation name: "javax.websocket-api-1.1"
    implementation name: "tomcat-websocket-8.5.33"
    implementation name: "tomcat-util-8.5.33"
    implementation name: "tomcat-juli-8.5.33"
    implementation name: "tomcat-api-8.5.33"

    // Jasper reports
    implementation name: "jasperreports-6.5.1"
    implementation name: "jasper-compiler-jdt-5.5.23" // javac
    implementation name: "commons-logging-1.2"
    implementation name: "commons-digester-2.1"
    implementation name: "commons-collections-3.2.2"
    implementation name: "commons-beanutils-1.9.3"
    implementation name: "itext-4.2.1"

    // Barbecue barcode generator
    implementation name: "barbecue-1.5-beta1"

    // JUnit
    testImplementation name : "junit-platform-commons-1.2.0"
    testImplementation name : "junit-platform-console-1.2.0"
    testImplementation name : "junit-platform-console-standalone-1.2.0"
    testImplementation name : "junit-platform-engine-1.2.0"
    testImplementation name : "junit-platform-gradle-plugin-1.2.0"
    testImplementation name : "junit-platform-launcher-1.2.0"
    testImplementation name : "junit-platform-runner-1.2.0"
    testImplementation name : "junit-platform-suite-api-1.2.0"
    testImplementation name : "junit-platform-surefire-provider-1.2.0"
    testImplementation name : "junit-jupiter-api-5.2.0"
    testImplementation name : "junit-jupiter-engine-5.2.0"
    testImplementation name : "junit-jupiter-params-5.2.0"
    testImplementation name : "junit-jupiter-migrationsupport-5.2.0"
    testImplementation name : "junit-vintage-engine-5.2.0"
    testImplementation name : "apiguardian-api-1.0.0"
    testImplementation name : "opentest4j-1.1.0"
}

processResources {
    filesMatching('**/config.properties') {
        def env = System.getenv('build-environemnt')
        println 'build-environment = ' + env

        filter(ReplaceTokens, tokens:['APP_VERSION': project.version])

        /*if(env == 'INT') filter(ReplaceTokens, tokens:['APP_VERSION': project.version + '-SNAPSHOT' + new Date().format('yyyyMMddHHmmss')])
        else if(env == 'PROD') filter(ReplaceTokens, tokens:['APP_VERSION': project.version])
        else *//*(DEV)*//* filter(ReplaceTokens, tokens:['APP_VERSION': project.version + '-DEV'])*/
    }
}

jar {
    manifest {
        attributes('Implementation-Title': project.name,
                'Implementation-Version': project.version,
                'Implementation-Vendor': 'NIC',
                'Application-Name': 'Biometrics Workstation',
                'Built-By': 'Biometrics Team',
                'JavaFX-Preloader-Class': 'sa.gov.nic.bio.bw.client.preloader.AppPreloader',
                'JavaFX-Application-Class': 'sa.gov.nic.bio.bw.client.AppEntryPoint',
                'Main-Class': 'sa.gov.nic.bio.bw.client.AppEntryPoint',
                'Class-Path': configurations.runtimeClasspath.files.collect { it.getName() }.join(' '))
    }
}

task collectDependencies(type: Copy) {
    from configurations.runtimeClasspath.files
    into 'build/libs'
}
collectDependencies.dependsOn(jar)

task packageWebStart(group: 'web-start') {
    doLast {

        String jnlpTemplate = new File("build-resources/bw-template.jnlp").getText('UTF-8')

        File directory = new File("${projectDir}/build/web-start")
        directory.mkdirs()
        File file = new File(directory, 'APPLICATION.JNLP')
        file.createNewFile()
        file.text = jnlpTemplate

        // copy jar files
        copy {
            from 'build/libs'
            into "build/web-start/classpath-jars"
        }

        // compress the jars
        fileTree(dir: 'build/web-start/classpath-jars', include: '**/*.jar', excludes: ['bw-client-*.jar', 'controlsfx-*.jar']).visit { FileVisitDetails details ->
            String fileName = details.file.name
            exec {
                workingDir 'build/web-start/classpath-jars'
                commandLine 'pack200', fileName + ".pack.gz", fileName
            }
        }

        // delete the un-compressed jars
        fileTree(dir: 'build/web-start/classpath-jars', include: '**/*.jar', excludes: ['bw-client-*.jar', 'controlsfx-*.jar']).visit { FileVisitDetails details ->
            details.file.delete()
        }

        // copy the extensions folder
        copy {
            from 'build/extensions'
            into "build/web-start/resources/extensions"
        }
    }
}
packageWebStart.dependsOn(collectDependencies)
run.dependsOn(collectDependencies)


remotes {
    webServer {
        host = '10.0.73.50'
        user = 'root'
        identity = file('build-resources/integration_server_id_rsa')
    }
}

task packageWebStartAndDeploy(group: 'web-start') {
    doLast {
        ssh.run {
            session(remotes.webServer) {
                remove '/opt/dev/wlp/usr/servers/bio-client-downloader/clients/bw'
                execute 'mkdir /opt/dev/wlp/usr/servers/bio-client-downloader/clients/bw'
                put from: new File('build/web-start').listFiles().toList(), into: '/opt/dev/wlp/usr/servers/bio-client-downloader/clients/bw'
            }
        }
    }
}
packageWebStartAndDeploy.dependsOn(packageWebStart)

task deploy(group: 'web-start') {
    doLast {
        ssh.run {
            session(remotes.webServer) {
                remove '/opt/dev/wlp/usr/servers/bio-client-downloader/clients/bw'
                execute 'mkdir /opt/dev/wlp/usr/servers/bio-client-downloader/clients/bw'
                put from: new File('build/web-start').listFiles().toList(), into: '/opt/dev/wlp/usr/servers/bio-client-downloader/clients/bw'
            }
        }
    }
}

task packageExtensions(type: Jar) {
    delete fileTree('build/extensions').include('**/*')
    from("build/classes/java/main") {
        include '/sa/gov/nic/bio/bw/client/core/utils/ArabicCalendarDataProvider.class'
    }

    from("build/resources/main") {
        include 'META-INF/**'
    }


    destinationDir file('build/extensions')
    baseName 'arabic-calendar-data-provider'
    version '1.0'
    extension 'jar'
}
packageExtensions.dependsOn(build)
run.dependsOn(packageExtensions)
packageWebStart.dependsOn(packageExtensions)